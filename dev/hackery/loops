#!/usr/bin/env perl
use strict;
use warnings;
use Benchmark qw/timethese/;

my @xs = map rand(100), 1..100;

timethese 400000, {
  forgrep => sub {
    my $i = 0;
    $i += $_*$_ for grep $_ > 50, @xs;
    $i;
  },

  formapgrep => sub {
    my $i = 0;
    $i += $_ for map $_*$_, grep $_ > 50, @xs;
    $i;
  },

  andfor => sub {
    my $i = 0;
    $_ > 50 and $i += $_*$_ for @xs;
    $i;
  },

  forif => sub {
    my $i = 0;
    for (@xs)
    {
      $i += $_*$_ if $_ > 50;
    }
    $i;
  },

  fornext => sub {
    my $i = 0;
    for (@xs)
    {
      next unless $_ > 50;
      $i += $_*$_;
    }
    $i;
  },

  foriif => sub {
    my $i = 0;
    for (0..$#xs)
    {
      $i += $xs[$_]*$xs[$_] if $xs[$_] > 50;
    }
    $i;
  },

  whileif => sub {
    my $i = 0;
    local $_ = 0;
    while ($_ < @xs)
    {
      $i += $xs[$_]*$xs[$_] if $xs[$_] > 50;
      ++$_;
    }
    $i;
  },

  gotoif => sub {
    my $i = 0;
    local $_ = -1;
    loop:
    goto end if ++$_ > $#xs;
    goto loop unless $xs[$_] > 50;
    $i += $xs[$_]*$xs[$_];
    goto loop;
    end:
    $i;
  },
};
